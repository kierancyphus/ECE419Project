plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'com.chickenrunfanclub'
version '1.3-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.16.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.16.0'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'org.apache.zookeeper:zookeeper:3.4.0'
    implementation 'commons-codec:commons-codec:1.15'
}

test {
    useJUnitPlatform()
}

run {
    standardInput = System.in
}

jar {
    manifest {
        attributes "Main-Class": "com.chickenrunfanclub.runner.Entrypoint"
    }
}

 application {
    mainClassName = 'com.chickenrunfanclub.runner.Entrypoint'
}

tasks.withType(Test) {
    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}